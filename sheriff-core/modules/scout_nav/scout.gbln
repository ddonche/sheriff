/// modules/scout_nav/scout.gbln
/// - TOC + breadcrumbs (page-scoped)
/// - Nav menus from nav.yall (portal-scoped)

/// ------------------------------------------------------------------------
/// Load ../site/portals/{portal}/nav.yall as a map.
/// If missing, return {} so callers don't explode.
/// ------------------------------------------------------------------------
act scout_load_nav_config(ctx)
    portal | ctx["portal"]

    if portal.nix?
        return {}
    xx

    path | "../site/portals/{portal}/nav.yall"

    if :file_exists(path) == false
        return {}
    xx

    return :yall_parse_file(path)
xx

/// ------------------------------------------------------------------------
/// Turn "password_generator_recipe.md.html" into "password generator recipe"
/// ------------------------------------------------------------------------
act slug_to_label(slug)
    label | "" + slug

    /// strip common extensions
    label |= ignore_where(label, ".md.html")
    label |= ignore_where(label, ".html")
    label |= ignore_where(label, ".md")

    /// replace "_" with " "
    label |= :join(:split(label, "_"), " ")

    /// replace "-" with " "
    label |= :join(:split(label, "-"), " ")

    return label
xx

/// ------------------------------------------------------------------------
/// Build TOC HTML from rendered page HTML.
/// Looks for <h1>…</h1>, <h2>…</h2>, <h3>…</h3>, <h4>…</h4>
/// and assumes an <a ... id="...">Text</a> pattern inside.
/// ------------------------------------------------------------------------
act generate_toc(html_content)
    headers | []

    sweep html_content
        "<h1>" ... "</h1>" :
            anchor_id | keep_between(self, "id=\"", "\"")
            text      | trim(keep_between(self, "</a>", "<"))

            if text != "" and anchor_id != ""
                put_last!(headers, {"level": 1, "id": anchor_id, "text": text})
            xx

        "<h2>" ... "</h2>" :
            anchor_id |= keep_between(self, "id=\"", "\"")
            text      |= trim(keep_between(self, "</a>", "<"))

            if text != "" and anchor_id != ""
                put_last!(headers, {"level": 2, "id": anchor_id, "text": text})
            xx

        "<h3>" ... "</h3>" :
            anchor_id |= keep_between(self, "id=\"", "\"")
            text      |= trim(keep_between(self, "</a>", "<"))

            if text != "" and anchor_id != ""
                put_last!(headers, {"level": 3, "id": anchor_id, "text": text})
            xx

        "<h4>" ... "</h4>" :
            anchor_id |= keep_between(self, "id=\"", "\"")
            text      |= trim(keep_between(self, "</a>", "<"))

            if text != "" and anchor_id != ""
                put_last!(headers, {"level": 4, "id": anchor_id, "text": text})
            xx
    xx

    /// If no headers, emit no TOC at all
    if headers.len == 0
        return ""
    xx

    toc_parts | []
    for h in headers
        level | h["level"]
        id    | h["id"]
        text  | h["text"]

        item | "<li class=\"toc-item toc-level-" + string(level)
        item |= item + "\"><a href=\"#"
        item |= item + id + "\">" + text + "</a></li>\n"

        put_last!(toc_parts, item)
    xx

    toc_html | pack(toc_parts)

    prefix |
        "<aside class=\"toc\" aria-label=\"On this page\">" +
        "<details class=\"toc-details\" open>" +
        "<summary class=\"toc-summary\">On this page</summary>" +
        "<ul class=\"toc-list\">\n"

    suffix | "</ul></details></aside>\n"

    return prefix + toc_html + suffix
xx

/// ------------------------------------------------------------------------
/// Breadcrumbs
/// - No "Home"
/// - Root "Docs" for anything under /docs
/// - Uses sheriff-breadcrumb-* classes to match CSS / React mock
/// ------------------------------------------------------------------------
act build_breadcrumbs(ctx)
    path   | ctx["path"]
    portal | ctx["portal"]

    if path.nix? or portal.nix?
        return ""
    xx

    content_root | "../site/portals/{portal}/content"
    rel          | :path_relative_to(path, content_root)

    /// Root → no breadcrumbs
    if rel == ""
        return ""
    xx

    segs       | :split(rel, "/")
    clean_segs | []

    /// Drop empty segments + trailing index.md
    for s in segs
        if s == "" => skip
        if s == "index.md" => skip
        :put_last!(clean_segs, s)
    xx

    if clean_segs.len == 0
        return ""
    xx

    parts | []

    /// Outer wrapper – matches .sheriff-breadcrumbs box
    :put_last!(parts,
        "<nav class=\"sheriff-breadcrumbs\" aria-label=\"Breadcrumb\">"
    )

    idx | 0
    for seg in clean_segs
        is_last | (idx == clean_segs.len - 1)

        /// Get a label and force it to string
        label | slug_to_label(seg)
        label |= "" + label

        /// Escape + Title-case
        safe | :escape_html(label)
        safe |= safe.title

        if is_last
            :put_last!(parts,
                "<div class=\"sheriff-breadcrumb-row\">" +
                    "<span class=\"sheriff-breadcrumb-arrow\">↳</span>" +
                    "<span class=\"sheriff-breadcrumb-current\">" + safe + "</span>" +
                "</div>"
            )
        else
            :put_last!(parts,
                "<div class=\"sheriff-breadcrumb-row\">" +
                    "<span class=\"sheriff-breadcrumb-arrow\">↳</span>" +
                    "<span class=\"sheriff-breadcrumb-link sheriff-link\">" + safe + "</span>" +
                "</div>"
            )
        xx

        idx | idx + 1
    xx

    :put_last!(parts, "</nav>")

    return :pack(parts)
xx

/// ------------------------------------------------------------------------
/// Main entry: called from the pipeline as scout::apply_tokens
/// After layout + wire_slots, before Brindle resolves tokens.
/// ------------------------------------------------------------------------
act apply_tokens(ctx)
    body | ctx["body"]
    if body.nix?
        return ctx
    xx

    /// Always clear first so nothing leaks between pages
    clear_token("SCOUT", "TOC")
    clear_token("SCOUT", "BREADCRUMBS")

    /// BREADCRUMBS (page-scoped)
    breadcrumbs_html | build_breadcrumbs(ctx)
    register_token("SCOUT", "BREADCRUMBS", breadcrumbs_html)

    /// TOC (page-scoped)
    toc_html | generate_toc(body)   /// should return "" when no headers
    register_token("SCOUT", "TOC", toc_html)

    /// Resolve SCOUT::* for this page only
    rendered | :resolve_token(body)
    :update!(ctx["body"], rendered)

    /// Clear page-scoped tokens for the next file
    clear_token("SCOUT", "TOC")
    clear_token("SCOUT", "BREADCRUMBS")
    return ctx
xx

/// ========================================================================
/// NAV MENUS (from nav.yall + theme scout_nav.yall)
/// ========================================================================

act make_abs_href(portal, raw)
    if raw.nix?
        return ""
    xx

    href | :trim("" + raw)

    if href == "" or href == "{}"
        return ""
    xx

    /// Check for absolute URLs (http/https)
    if :starts_with(href, "http://")
        return href
    xx
    
    if :starts_with(href, "https://")
        return href
    xx
    
    /// Already starts with /
    if :starts_with(href, "/")
        return href
    xx

    /// Relative path - make it absolute
    return "/" + href
xx

/// ------------------------------------------------------------------------
act generate_nested_items(portal, items_map)
    if items_map.nix?
        return ""
    xx

    vt | :valtype(items_map)
    if vt != "map"
        return ""
    xx

    keys | :keys(items_map)
    if keys.len == 0
        return ""
    xx

    ul_items | ""

    for key in keys
        val      | items_map[key]
        val_type | :valtype(val)

        if val_type == "map"
            nested_html | generate_nested_items(portal, val)
            if nested_html == ""
                skip
            xx

            item | """
            <li class="has-submenu">
              <details class="sheriff-submenu" open>
                <summary class="submenu-header">
                  <span>{key}</span>
                  <span class="submenu-icon" aria-hidden="true">▸</span>
                </summary>
                {nested_html}
              </details>
            </li>
            """

            update!(ul_items, ul_items + item)
        else
            href | make_abs_href(portal, val)
            if href == ""
                skip
            xx

            item | """
<li>
  <a class="sheriff-link" href="{href}">{key}</a>
</li>
"""
            update!(ul_items, ul_items + item)
        xx
    xx

    if ul_items == ""
        return ""
    xx

    html | """
<ul>
{ul_items}</ul>
"""
    return html
xx

/// ------------------------------------------------------------------------
/// Sidebar / nested docs tree renderer.
/// NOTE: HTML structure + CSS classes preserved from old implementation.
/// ------------------------------------------------------------------------
act scout_render_sidebar(portal, menu_content)
    if menu_content.nix?
        return ""
    xx

    vt | :valtype(menu_content)
    if vt != "map"
        return ""
    xx

    keys | :keys(menu_content)
    if keys.len == 0
        return ""
    xx

    nav_items | ""
    first_key | true

    for top_key in keys
        top_val   | menu_content[top_key]
        val_type  | :valtype(top_val)

        if first_key and val_type == "map"
            nested | generate_nested_items(portal, top_val)
            if nested == ""
                skip
            xx

            item | """
<div class="section-header">
  <span>{top_key}</span>
</div>
{nested}
"""
            update!(nav_items, nav_items + item)
            update!(first_key, false)

        elif val_type == "str"
            href | make_abs_href(portal, top_val)
            if href == ""
                skip
            xx

            item | """
<li>
  <a class="sheriff-link" href="{href}">{top_key}</a>
</li>
"""
            update!(nav_items, nav_items + item)
            update!(first_key, false)

        elif val_type == "map"
            nested | generate_nested_items(portal, top_val)
            if nested == ""
                skip
            xx

            item | """
            <li class="has-submenu">
              <details class="sheriff-submenu" open>
                <summary class="submenu-header">
                  <span>{top_key}</span>
                  <span class="submenu-icon" aria-hidden="true">▸</span>
                </summary>
                {nested}
              </details>
            </li>
            """

            update!(nav_items, nav_items + item)
            update!(first_key, false)
        xx
    xx

    if nav_items == ""
        return ""
    xx

    html | """
<aside class="sheriff-left-nav">
{nav_items}
</aside>
"""
    return html
xx

/// ------------------------------------------------------------------------
/// Footer flat links – dead simple & safe.
/// ------------------------------------------------------------------------
act scout_render_footer(portal, footer_content)
    if footer_content.nix?
        return ""
    xx

    vt | :valtype(footer_content)
    if vt != "map"
        return ""
    xx

    keys | :keys(footer_content)
    if keys.len == 0
        return ""
    xx

    footer_links | ""
    first        | true

    for text in keys
        url | footer_content[text]
        
        /// Separator between items
        sep | ""
        if first == false
            update!(sep, " · ")
        xx

        /// Decide: plain text vs link
        is_link | true
        url_str | ""

        if url.nix?
            update!(is_link, false)
        else
            tmp | "" + url
            tmp |= :trim(tmp)

            if tmp == "" or tmp == "{}"
                update!(is_link, false)
            else
                update!(url_str, make_abs_href(portal, tmp))
                if url_str == ""
                    update!(is_link, false)
                xx
            xx
        xx

        item | ""
        if is_link
            update!(item, "{sep}<a href=\"{url_str}\" class=\"sheriff-link\">{text}</a>")
        else
            update!(item, "{sep}<span>{text}</span>")
        xx

        update!(footer_links, footer_links + item)
        update!(first, false)
    xx

    if footer_links == ""
        return ""
    xx

    html | """
<div class="sheriff-footer-links">
{footer_links}</div>
"""
    return html
xx

/// ------------------------------------------------------------------------
/// Header nav – CSS & structure preserved.
/// ------------------------------------------------------------------------
act scout_render_header_nav(portal, menu_content)
    if menu_content.nix?
        return ""
    xx

    vt | :valtype(menu_content)
    if vt != "map"
        return ""
    xx

    keys | :keys(menu_content)
    if keys.len == 0
        return ""
    xx

    nav_items | ""

    for top_key in keys
        top_val   | menu_content[top_key]
        val_type  | :valtype(top_val)

        if val_type == "str"
            href | make_abs_href(portal, top_val)
            if href == ""
                skip
            xx

            item | """
<li>
  <a class="sheriff-link" href="{href}">{top_key}</a>
</li>
"""
            update!(nav_items, nav_items + item)

        elif val_type == "map"
            dropdown_html | generate_header_dropdown(portal, top_val)
            if dropdown_html == ""
                skip
            xx

            item | """
<li>
  <span class="sheriff-link">{top_key} <span class="dropdown-arrow">▼</span></span>
  <div class="dropdown-panel">
    {dropdown_html}
  </div>
</li>
"""
            update!(nav_items, nav_items + item)
        xx
    xx

    if nav_items == ""
        return ""
    xx

    html | """
<nav class="sheriff-header-nav">
  <ul>
{nav_items}  </ul>
</nav>
"""
    return html
xx

/// ------------------------------------------------------------------------
act generate_header_dropdown(portal, dropdown_map)
    if dropdown_map.nix?
        return ""
    xx

    keys | :keys(dropdown_map)
    if keys.len == 0
        return ""
    xx

    sections_html | ""

    for section_key in keys
        section_val  | dropdown_map[section_key]
        section_type | :valtype(section_val)

        if section_type == "str"
            href | make_abs_href(portal, section_val)
            if href == ""
                skip
            xx

            section_html | """
<div class="dropdown-section">
  <a href="{href}">{section_key}</a>
</div>
"""
            update!(sections_html, sections_html + section_html)

        elif section_type == "map"
            links_html | ""
            link_keys  | :keys(section_val)

            for link_key in link_keys
                link_href | make_abs_href(portal, section_val[link_key])
                if link_href == ""
                    skip
                xx

                link_item | """
    <li>
      <a href="{link_href}">{link_key}</a>
    </li>
"""
                update!(links_html, links_html + link_item)
            xx

            if links_html != ""
                section_html | """
<div class="dropdown-section">
  <span class="dropdown-section-title">{section_key}</span>
  <ul>
{links_html}  </ul>
</div>
"""
                update!(sections_html, sections_html + section_html)
            xx
        xx
    xx

    return sections_html
xx

/// ------------------------------------------------------------------------
/// Built-in menu kinds:
///   - "header"      → header dropdown
///   - "sidebar"     → nested docs tree
///   - "footer_flat" → compact footer links
/// Unknown kinds currently fall back to "sidebar".
/// ------------------------------------------------------------------------
act scout_render_menu_for_kind(portal, menu_map, kind)
    if menu_map.nix?
        return ""
    xx

    vt | :valtype(menu_map)
    if vt != "map"
        return ""
    xx

    k | "" + kind

    if k == "header"
        return scout_render_header_nav(portal, menu_map)
    xx

    if k == "footer_flat"
        return scout_render_footer(portal, menu_map)
    xx

    /// Default / sidebar
    return scout_render_sidebar(portal, menu_map)
xx

/// ------------------------------------------------------------------------
/// Determine active theme name from portal config.
/// Defaults to "outpost" if missing.
/// ------------------------------------------------------------------------
act scout_active_theme_name(portal)
    cfg_path | "../site/portals/{portal}/config.yall"
    if :file_exists(cfg_path) == false
        return "outpost"
    xx

    cfg   | :yall_parse_file(cfg_path)
    theme | cfg["theme"]
    if theme.nix?
        return "outpost"
    xx

    name | theme["name"]
    if name.nix?
        return "outpost"
    xx

    return "" + name
xx

/// ------------------------------------------------------------------------
/// Load theme-specific nav mapping:
/// - Portal config:  ../site/portals/{portal}/config.yall
///     theme:
///       name: some_theme
/// - Theme mapping:  ../themes/{theme}/scout_nav.yall
///
/// If any of this is missing, we HARD FAIL on purpose.
/// ------------------------------------------------------------------------
act scout_load_theme_nav_mapping(portal)
    cfg_path | "../site/portals/{portal}/config.yall"
    if :file_exists(cfg_path) == false
        :say("SCOUT: Missing portal config.yall for portal '{portal}'")
        return nil
    xx

    cfg   | :yall_parse_file(cfg_path)
    theme | cfg["theme"]
    if theme.nix?
        :say("SCOUT: Portal '{portal}' has no theme entry in config.yall")
        return nil
    xx

    name | theme["name"]
    if name.nix?
        :say("SCOUT: Portal '{portal}' has theme section but no theme.name")
        return nil
    xx

    theme_name | "" + name

    /// FIXED: theme file must be INSIDE THE PORTAL
    map_path | "../sheriff-core/themes/{theme_name}/scout_nav.yall"

    if :file_exists(map_path) == false
        :say("SCOUT: Theme '{theme_name}' missing scout_nav.yall at " ++ map_path)
        return nil
    xx

    cfg2    | :yall_parse_file(map_path)
    mapping | cfg2["tokens"]
    if mapping.nix?
        :say("SCOUT: Theme '{theme_name}' has no tokens section in scout_nav.yall")
        return nil
    xx

    return mapping
xx

/// ------------------------------------------------------------------------
/// Publish SCOUT::* nav tokens from nav.yall + theme mapping.
/// For each menu in nav.yall, we generate:
///   SCOUT::<name>_DROPDOWN  → header dropdown
///   SCOUT::<name>_SIDEBAR   → sidebar tree
///   SCOUT::<name>_FLAT      → footer-style flat links
///
/// Then we apply the theme mapping (scout_nav.yall):
///   tokens:
///     FOOTER_LEFT:
///       menu: footer_left
///       kind: footer_flat
///     FOOTER_RIGHT:
///       menu: footer_right
///       kind: footer_flat
///   etc.
/// ------------------------------------------------------------------------
act publish_nav_tokens(pre_ctx)
    /// Defensive: if some earlier pre action returned nil, don't explode.
    if pre_ctx.nix?
        :say("SCOUT: pre_ctx is nil in publish_nav_tokens; skipping menus.")
        return pre_ctx
    xx

    portal | pre_ctx["portal"]
    if portal.nix?
        :say("SCOUT: No portal in pre_ctx. Skipping menus.")
        return pre_ctx
    xx
    
    nav_path | "../site/portals/{portal}/nav.yall"
    if :file_exists(nav_path) == false
        :say("SCOUT: No nav.yall for portal '{portal}'. Skipping menus.")
        return pre_ctx
    xx

    nav_cfg | :yall_parse_file(nav_path)
    menus   | nav_cfg["menus"]
    if menus.nix?
        :say("SCOUT: nav.yall has no menus section. Skipping menus.")
        return pre_ctx
    xx

    names | :keys(menus)

    /// Generic per-menu tokens (header/sidebar/flat)
    for name in names
        menu_map | menus[name]
        if menu_map.nix?
            :say("SCOUT: menu '" ++ ("" + name) ++ "' is nil, skipping.")
            skip
        xx

        /// 1) Header dropdown token: <name>_DROPDOWN
        header_html | scout_render_header_nav(portal, menu_map)
        if header_html != ""
            header_token | ("" + name) + "_DROPDOWN"
            register_token("SCOUT", header_token, header_html)
        xx

        /// 2) Sidebar token: <name>_SIDEBAR
        sidebar_html | scout_render_sidebar(portal, menu_map)
        if sidebar_html != ""
            sidebar_token | ("" + name) + "_SIDEBAR"
            register_token("SCOUT", sidebar_token, sidebar_html)
        xx

        /// 3) Flat footer token: <name>_FLAT
        footer_html | scout_render_footer(portal, menu_map)
        if footer_html != ""
            footer_token | ("" + name) + "_FLAT"
            register_token("SCOUT", footer_token, footer_html)
        xx
    xx

    /// IMPORTANT: always hand the ctx back
    return pre_ctx
xx