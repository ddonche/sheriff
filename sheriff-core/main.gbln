/// sheriff-core/main.gbln
import "../site/portals/default/manifest.imports"

/// Load events.yall into a map via Y'all
act load_events(portal)
    path | "../site/portals/{portal}/events.yall"
    return :yall_parse_file(path)
xx

act create_all_dirs(path)
    /// Ensure all directories in the path exist
    slash_positions | :find_all(path, "/")
    for slash_pos in slash_positions
        dir_part | path[0:slash_pos]
        if dir_part != "" and :file_exists(dir_part) == false
            :create_dir!(dir_part)
        xx
    xx
xx

/// MAIN BUILD FUNCTION
act build(portal)
    :say("Building portal: {portal}")
    
    cfg      | load_events(portal)
    schedule | cfg["schedule"]   /// Array of action names
    
    content_root | "../site/portals/{portal}/content"
    md_files     | []
    
    for f in :walk(content_root, "**/*.md")
        :put_last!(md_files, f)
    xx

    /// Run pre-build schedule once
    pre_schedule | cfg["pre"]
    if !pre_schedule.nix?
        pre_ctx | {
            "portal": portal
        }
        :summon(pre_ctx, pre_schedule)
    xx
    
    for md_path in md_files
        raw | :read_text(md_path)
        
        ctx | {
            "body": raw,
            "frontmatter": nil,
            "tokens": {},
            "path": md_path,
            "portal": portal
        }
                
        ctx |= :summon(ctx, schedule)
        
        rel_path | :path_relative_to(md_path, content_root)  /// "docs/judge.md"
        dir      | :dirname(rel_path)                         /// "docs"
        base     | :stem(rel_path)                            /// "judge"
        out_file | "../dist/{portal}/{dir}/{base}.html"      /// "../dist/default/docs/judge.html"
        
        create_all_dirs(out_file)
        
        :write_text!(out_file, ctx["body"])
    xx
    
    :say("Portal build finished: {portal}")
xx

build("default")