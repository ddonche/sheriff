/// sheriff-core/main.gbln
/// import "../site/portals/sheriff/manifest.imports"
import "../sheriff-core/manifest.imports"

//// Load events.yall into a map via Y'all
act load_events(portal)
    path | "../site/portals/{portal}/events.yall"
    return :yall_parse_file(path)
xx
////

act reset_slots()
    clear_token("SLOT", "STYLELINK")
    clear_token("SLOT", "LOGO")
    clear_token("SLOT", "BRANDNAME")
    clear_token("SLOT", "VERSION")
    clear_token("SLOT", "THEMESWITCHER")
    clear_token("SLOT", "VERSIONSWITCHER")
    clear_token("SLOT", "SEARCH")
    clear_token("SLOT", "REPO")

    clear_token("SLOT", "HEADER_NAV")
    clear_token("SLOT", "LEFT_NAV")
    clear_token("SLOT", "SIDEBAR_TOP")
    clear_token("SLOT", "SIDEBAR_MAIN")

    clear_token("SLOT", "FOOTER_LEFT")
    clear_token("SLOT", "FOOTER_RIGHT")

    clear_token("SLOT", "HEADERLEFT")
    clear_token("SLOT", "HEADERRIGHT")
    clear_token("SLOT", "PAGEMETALEFT")
    clear_token("SLOT", "PAGEMETARIGHT")

    clear_token("SLOT", "SCRIPTS")
xx

act load_events(portal)
    return :yall_parse_file("../sheriff-core/events.yall")
xx

act create_all_dirs(path)
    /// Ensure all directories in the path exist
    slash_positions | :find_all(path, "/")
    for slash_pos in slash_positions
        dir_part | path[0:slash_pos]
        if dir_part != "" and :file_exists(dir_part) == false
            :create_dir!(dir_part)
        xx
    xx
xx

/// MAIN BUILD FUNCTION
act build(portal)
    :say("Building portal:")
    :say(portal)

    cfg      | load_events(portal)
    schedule | cfg["schedule"]   /// Array of action names

    content_root | "../site/portals/{portal}/content"
    md_files     | []

    for f in :walk(content_root, "**/*.md")
        :put_last!(md_files, f)
    xx

    /// Run pre-build schedule once (manual step-through mode)
    pre_schedule | cfg["pre"]
    if !pre_schedule.nix?
        pre_ctx | {
            "portal": portal
        }

        for act_name in pre_schedule
            :say("---- RUNNING PRE ACT ----")
            :say(act_name)

            /// Run exactly ONE action via summon
            ret | :summon(pre_ctx, [act_name])

            if ret.nix?
                :say("!!!! THIS ACT RETURNED NIL !!!!")
                :say(act_name)
                /// Keep old pre_ctx
            else
                pre_ctx | ret
            xx

        xx

    xx

    /// BUILD EACH MARKDOWN FILE
    for md_path in md_files
        raw | :read_text(md_path)

        ctx | {
            "body": raw,
            "frontmatter": {}, 
            "tokens": {},
            "path": md_path,
            "portal": portal
        }
        reset_slots()
        ctx |= :summon(ctx, schedule)

        rel_path | :path_relative_to(md_path, content_root)  /// "docs/judge.md"
        dir      | :dirname(rel_path)                        /// "docs"
        base     | :stem(rel_path)                           /// "judge"
        out_file | "../dist/{portal}/{dir}/{base}.html"      

        create_all_dirs(out_file)
        :write_text!(out_file, ctx["body"])
    xx

    :say("Portal build finished:")
    :say(portal)
xx

act discover_portals()
    root | "../site/portals"

    if :file_exists(root) == false or :is_dir(root) == false
        return []
    xx

    portals | []

    /// list_dirs returns ONLY directory names (e.g. "goblin", "sheriff")
    for name in :list_dirs(root)
        portal | :trim("" + name)

        if portal == "" or portal == "." or portal == ".."
            skip
        xx

        /// optional: ignore hidden-ish folders
        if portal[0:1] == "."
            skip
        xx

        /// double-check it's still a dir (paranoia, but cheap)
        if :is_dir(:path_join(root, portal)) == false
            skip
        xx

        :put_last!(portals, portal)
    xx

    return portals
xx

/// sheriff-core/main.gbln
/// Requires a portal argument:
///   goblin run main.gbln <portal>

if args.nix? or args.len == 0
    :say("ERROR: missing portal name")
    :say("Usage: goblin run main.gbln <portal|all>")
else
    cmd | args[0]

    if cmd == "all"
        portals | discover_portals()

        if portals.nix? or portals.len == 0
            :say("ERROR: no portals found under ../site/portals")
        else
            for p in portals
                :say("")
                :say("=== BUILD ALL:" ++ p ++ "===")
                build(p)
            xx
        xx
    else
        build(cmd)
    xx
xx