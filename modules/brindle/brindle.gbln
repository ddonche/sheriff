/// Brindle — auto-discover themes, register tokens, no building
act basename(p)
    parts = split(p, "/")
    if parts.len == 0
        return p
    end
    return parts[parts.len - 1]
end

act strip_css_ext(filename)
    if filename.len >= 4
        tail = filename[filename.len - 4:filename.len].lower
        if tail == ".css"
            return filename[0:filename.len - 4]
        end
    end
    return filename
end

/// Read desired theme name from goblin.yaml (or return nil if none)
act selected_theme_from_config()
    if file_exists("goblin.yaml") == false
        return nil
    end
    cfg_text = read_text("goblin.yaml")
    sanitized = yaml_sanitize(cfg_text)
    cfg = yaml_parse(sanitized)
    
    if cfg == nil
        return nil
    end
    br = cfg["brindle"]
    if br == nil
        return nil
    end
    t = br["theme"]
    if t == nil
        return nil
    end
    trimmed = ("" + t).trim
    if trimmed == ""
        return nil
    end
    return trimmed
end

/// Brindle — build_css_files (no safety rails, assumes required files exist)
act build_css_files()
    /// Always ensure destination directories exist
    create_dir!("dist/modules/brindle/themes")

    /// Copy baseline.css unconditionally
    baseline_src  = "modules/brindle/baseline.css"
    baseline_dest = "dist/modules/brindle/baseline.css"
    baseline_content = read_text(baseline_src)
    write_text!(baseline_dest, baseline_content)
    say "Brindle: copied baseline.css"

    /// Copy all theme files from /modules/brindle/themes → /dist/modules/brindle/themes
    theme_files = walk("modules/brindle/themes", "**/*.css")
    for theme_file in theme_files
        normalized   = path_normalize(theme_file)
        parts        = split(normalized, "/")
        filename     = parts[parts.len - 1]
        output_path  = "dist/modules/brindle/themes/" + filename

        theme_content = read_text(theme_file)
        write_text!(output_path, theme_content)
        say "Brindle: copied " + filename
    end
end

act publish_syntax_tokens()
    head_html = raw '''
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/styles/monokai-sublime.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/highlight.min.js"></script>
'''

    foot_html = raw '''
<script>
  document.addEventListener('DOMContentLoaded', function () {
    if (!window.hljs) return;
    // Let Highlight.js auto-detect all <pre><code> / <code> blocks
    window.hljs.highlightAll();
  });
</script>
'''

    register_token("BRINDLE", "HIGHLIGHT_HEAD", head_html)
    register_token("BRINDLE", "HIGHLIGHT_FOOT", foot_html)
    return Unit
end

/// Register BRINDLE::<THEME> for every file in modules/brindle/themes/*.css
act publish_theme_tokens()
    theme_dir = "modules/brindle/themes"
    if file_exists(theme_dir) == false
        return 0
    end

    files = walk(theme_dir, "**/*.css")
    file_count = 0

    for f in files
        base = basename(f)               /// e.g., "obsidian.css"
        stem = strip_css_ext(base)       /// e.g., "obsidian"
        if ("" + stem).trim == ""
            skip
        end
        token = upper(stem)              /// "OBSIDIAN"
        href  = "/modules/brindle/themes/" + stem + ".css"
        register_token("BRINDLE", token, href)
        file_count = file_count + 1
    end

    return file_count
end

/// Publish static tokens (BASELINE + all themes) and wire BRINDLE::CSS
act publish_tokens()
    baseline_href = "/modules/brindle/baseline.css"
    
    if file_exists("modules/brindle/baseline.css") == false
        register_token("BRINDLE", "CSS", "<!-- ERROR: baseline.css not found -->")
    else
        publish_theme_tokens()
        publish_brand_tokens()
        publish_syntax_tokens()

        /// --- theme toggle UI + script -------------------------------------
        toggle_html = raw '''
        <button id="theme-toggle" class="btn theme-toggle" type="button" aria-label="Toggle theme">
            <svg class="theme-icon theme-icon-light" width="16" height="16" viewBox="0 0 16 16" fill="currentColor">
                <path d="M8 12a4 4 0 1 1 0-8 4 4 0 0 1 0 8zM8 0a.75.75 0 0 1 .75.75v1.5a.75.75 0 0 1-1.5 0V.75A.75.75 0 0 1 8 0zm0 13a.75.75 0 0 1 .75.75v1.5a.75.75 0 0 1-1.5 0v-1.5A.75.75 0 0 1 8 13zM2.343 2.343a.75.75 0 0 1 1.061 0l1.06 1.061a.75.75 0 0 1-1.06 1.06l-1.06-1.06a.75.75 0 0 1 0-1.06zm9.193 9.193a.75.75 0 0 1 1.06 0l1.061 1.06a.75.75 0 0 1-1.06 1.061l-1.061-1.06a.75.75 0 0 1 0-1.061zM16 8a.75.75 0 0 1-.75.75h-1.5a.75.75 0 0 1 0-1.5h1.5A.75.75 0 0 1 16 8zM3 8a.75.75 0 0 1-.75.75H.75a.75.75 0 0 1 0-1.5h1.5A.75.75 0 0 1 3 8zm10.657-5.657a.75.75 0 0 1 0 1.061l-1.061 1.06a.75.75 0 1 1-1.06-1.06l1.06-1.06a.75.75 0 0 1 1.06 0zm-9.193 9.193a.75.75 0 0 1 0 1.06l-1.06 1.061a.75.75 0 0 1-1.061-1.06l1.06-1.061a.75.75 0 0 1 1.061 0z"/>
            </svg>
            <svg class="theme-icon theme-icon-dark" width="16" height="16" viewBox="0 0 16 16" fill="currentColor">
                <path d="M6 .278a.768.768 0 0 1 .08.858 7.208 7.208 0 0 0-.878 3.46c0 4.021 3.278 7.277 7.318 7.277.527 0 1.04-.055 1.533-.16a.787.787 0 0 1 .81.316.733.733 0 0 1-.031.893A8.349 8.349 0 0 1 8.344 16C3.734 16 0 12.286 0 7.71 0 4.266 2.114 1.312 5.124.06A.752.752 0 0 1 6 .278z"/>
            </svg>
        </button>
        '''

        theme_script = raw '''
<script>
  (function () {
    var THEME_KEY = 'goblin-theme';
    var LIGHT = '/modules/brindle/themes/daybreak.css';
    var DARK  = '/modules/brindle/themes/obsidian.css';

    var link   = document.getElementById('brindle-theme');
    var button = document.getElementById('theme-toggle');
    if (!link || !button) return;

    function applyTheme(theme) {
      var href = (theme === 'dark') ? DARK : LIGHT;
      link.setAttribute('href', href);
      document.documentElement.setAttribute('data-theme', theme);
      button.setAttribute('data-theme', theme);
      
      // Toggle icon visibility
      var lightIcon = button.querySelector('.theme-icon-light');
      var darkIcon = button.querySelector('.theme-icon-dark');
      if (theme === 'dark') {
        lightIcon.style.display = 'inline-block';
        darkIcon.style.display = 'none';
      } else {
        lightIcon.style.display = 'none';
        darkIcon.style.display = 'inline-block';
      }
    }

    var stored = null;
    try {
      stored = window.localStorage.getItem(THEME_KEY);
    } catch (e) {}

    var defaultTheme = link.getAttribute('data-default-theme') || 'light';
    var initial = (stored === 'dark' || stored === 'light') ? stored : defaultTheme;

    applyTheme(initial);

    button.addEventListener('click', function () {
      var current = button.getAttribute('data-theme') || initial;
      var next = (current === 'dark') ? 'light' : 'dark';

      try {
        window.localStorage.setItem(THEME_KEY, next);
      } catch (e) {}

      applyTheme(next);
    });
  })();
</script>
'''

        register_token("BRINDLE", "THEME_TOGGLE", toggle_html)
        register_token("BRINDLE", "THEME_SCRIPT", theme_script)
        /// -------------------------------------------------------------------

        chosen = selected_theme_from_config()
        
        if chosen !== nil
            chosen_lo = lower(chosen.trim)
            chosen_fs = "modules/brindle/themes/" + chosen_lo + ".css"
            
            if file_exists(chosen_fs)
                theme_href = "/modules/brindle/themes/" + chosen_lo + ".css"

                /// Decide default theme flag for JS based on chosen file
                default_theme = "light"
                if chosen_lo == "obsidian"
                    default_theme = "dark"
                end

                /// baseline FIRST, theme LAST so theme wins
                css_links =
                    "<link rel=\"stylesheet\" href=\"" + baseline_href + "\">\n" +
                    "    <link id=\"brindle-theme\" rel=\"stylesheet\" href=\"" +
                    theme_href + "\" data-default-theme=\"" + default_theme + "\">"

                register_token("BRINDLE", "CSS", css_links)
            else
                css_links = "<link rel=\"stylesheet\" href=\"" + baseline_href + "\">"
                register_token("BRINDLE", "CSS", css_links)
            end
        else
            /// No theme configured — keep old behavior: baseline only.
            css_links = "<link rel=\"stylesheet\" href=\"" + baseline_href + "\">"
            register_token("BRINDLE", "CSS", css_links)
        end
    end
end

/// --- helpers to read config safely ----------------------------------------
act brindle_cfg()
    if file_exists("goblin.yaml") == false
        return {}
    end

    config_text     = read_text("goblin.yaml")
    sanitized_text  = yaml_sanitize(config_text)
    parsed_config   = yaml_parse(sanitized_text)
    if parsed_config == nil
        return {}
    end
    return parsed_config
end

act cfg_str(map_value, key_name, default_value)
    if map_value == nil or valtype(map_value) !== "map"
        return default_value
    end

    raw_value = map_value[key_name]
    if raw_value == nil
        return default_value
    end

    string_value = ("" + raw_value).trim
    if string_value == ""
        return default_value
    end
    return string_value
end

/// --- publish brand tokens (root-absolute paths) ---------------------------
/// goblin.yaml:
/// brindle:
///   brand_name: "Goblin"
///   logo: "/public/logo.svg"     # optional override
///   favicon: "/public/favicon.ico"
act publish_brand_tokens()
    config_map      = brindle_cfg()
    brindle_section = config_map["brindle"]

    brand_name = cfg_str(brindle_section, "brand_name", "Site")
    brand_logo = cfg_str(brindle_section, "logo", "Logo")
    brand_favicon = cfg_str(brindle_section, "favicon", "Favicon")

    /// Hard paths, root-absolute so they work from any depth
    register_token("BRINDLE", "FAVICON",   "/public/{brand_favicon}")
    register_token("BRINDLE", "BRAND_NAME", brand_name)
    register_token("BRINDLE", "LOGO_SRC",  "/public/{brand_logo}")
    register_token("BRINDLE", "BRAND_ALT", brand_name)

    /// Composite <img> tag (root-absolute src)
    register_token(
        "BRINDLE",
        "LOGO_IMG",
        "<img class=\"brand-logo\" src=\"/public/{brand_logo}\" alt=\"" + brand_name + "\" />"
    )

    return Unit
end