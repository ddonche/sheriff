/// modules/scout/scout.gbln — Scout: read YAML → canon → render HTML → write partials → register tokens

/// ---- filesystem + string helpers -----------------------------------------
act scout_to_html_href(path)
    clean_path = trim(string(path))
    if clean_path == ""
        return ""
    end

    lower_path = lower(clean_path)
    if starts_with(lower_path, "http://") or starts_with(lower_path, "https://") or starts_with(lower_path, "mailto:") or starts_with(clean_path, "#")
        return clean_path
    end

    norm = clean_path
    if norm[0:1] == "/"
        return norm
    end
    return "/" + norm
end

/// Ensure all directories in the given path exist
act scout_create_all_dirs(path)
    slash_positions = find_all(path, "/")

    for slash_pos in slash_positions
        dir_part = path[0:slash_pos]
        if dir_part !== "" and file_exists(dir_part) == false
            create_dir!(dir_part)
        end
    end
end

act scout_partial_path(token_key)
    key_lower = lower(token_key)
    return "dist/modules/scout/partials/scout_" + key_lower + ".html"
end

act scout_as_str(any_value)
    if any_value == nil
        return ""
    end
    return string(any_value)
end

act scout_as_array(any_value)
    if any_value == nil
        return []
    end
    value_type = valtype(any_value)
    if value_type == "array" <> value_type == "seq"
        return any_value
    end
    boxed = []
    put_last!(boxed, any_value)
    return boxed
end

/// ---- Scout: Breadcrumbs --------------------------------------------------

act title_from_slug(slug)
    s = string(slug)
    s = ignore_where(s, ".html")
    words = split(s, "_")
    label = join(words, " ")
    return title(label)
end

/// Build breadcrumb HTML from an output path like "dist/docs/guide/getting_started.html"
act breadcrumbs(out_path, root_label)
    if root_label == nil
        root_label = "Home"
    end

    /// Normalize input
    local p = string(out_path)
    p = ignore_where(p, "dist/")
    if p !== "" and p[0:1] == "/"
        p = p[1:]
    end

    /// Split into segments
    local segs = []
    for s in split(p, "/")
        if s !== ""
            put_last!(segs, s)
        end
    end

    /// Drop trailing index.html
    if segs.len > 0
        local last_seg = lower(segs[segs.len - 1])
        if last_seg == "index.html"
            segs = segs[0:segs.len - 1]
        end
    end

    /// Root page? No breadcrumbs.
    if segs.len == 0
        register_token("SCOUT", "BREADCRUMBS", "")
        return ""
    end

    /// Build HTML using put_last! + pack() — LIKE scout_render_list()
    local html_parts = []

    /// Opening
    put_last!(html_parts, "<nav class=\"breadcrumbs\" aria-label=\"Breadcrumb\"><ol>")

    /// Home link
    put_last!(html_parts, "<li><a href=\"/\">" + escape_html(root_label) + "</a></li>")

    /// Build each segment
    local current_href = ""
    local i = 0
    for seg in segs
        local is_last = (i == segs.len - 1)
        local stem = ignore_where(seg, ".html")
        local label = title_from_slug(stem)

        current_href = current_href + "/" + seg
        local href = current_href

        /// Add trailing slash if no .html
        local html_hits = find_all(href, ".html")
        if html_hits.len == 0
            href = href + "/"
        end

        if is_last
            put_last!(html_parts, "<li aria-current=\"page\"><span>" + escape_html(label) + "</span></li>")
        else
            put_last!(html_parts, "<li><a href=\"" + escape_html(href) + "\">" + escape_html(label) + "</a></li>")
        end

        i = i + 1
    end

    /// Closing
    put_last!(html_parts, "</ol></nav>")

    /// Pack and return
    local breadcrumbs_html = pack(html_parts)
    
    /// Register the breadcrumbs as a token
    register_token("SCOUT", "BREADCRUMBS", breadcrumbs_html)
    
    return breadcrumbs_html
end

/// ---- 1) canonicalize one entry -------------------------------------------
act scout_canon_entry(raw_entry)
    entry_type = valtype(raw_entry)

    if entry_type == "array" <> entry_type == "seq"
        parts = scout_as_array(raw_entry)
        if parts.len >= 2
            return {
                "label": scout_as_str(parts[0]),
                "href":  scout_as_str(parts[1]),
                "children": []
            }
        end
        return nil
    end

    if entry_type == "map" <> entry_type == "object"
        label_text = scout_as_str(raw_entry >> "label")
        href_text  = scout_as_str(raw_entry >> "href")
        kids_raw   = raw_entry >> "children"
        kids_list  = []

        for child_raw in scout_as_array(kids_raw)
            child_norm = scout_canon_entry(child_raw)
            if child_norm != nil
                put_last!(kids_list, child_norm)
            end
        end

        /// ignore maps with no label/href/children
        if label_text == "" and href_text == "" and kids_list.len == 0
            return nil
        end

        return {
            "label": label_text,
            "href":  href_text.trim,
            "children": kids_list
        }
    end

    return nil
end

/// ---- 2) canonicalize whole nav map ---------------------------------------
act scout_canon_map(raw_map)
    out_map  = { "entries": [] }
    map_type = valtype(raw_map)

    /// top-level: array/seq → each item is an entry
    if map_type == "array" <> map_type == "seq"
        for raw_entry in raw_map
            normalized_entry = scout_canon_entry(raw_entry)
            if normalized_entry != nil
                put_last!(out_map >> "entries", normalized_entry)
            end
        end
        return out_map
    end

    /// top-level: map/object
    if map_type == "map" <> map_type == "object"
        /// if an explicit 'entries' array exists, use it
        if has(raw_map, "entries")
            ent_any  = raw_map >> "entries"
            ent_type = valtype(ent_any)
            if ent_type == "array" <> ent_type == "seq"
                for raw_entry in ent_any
                    normalized_entry = scout_canon_entry(raw_entry)
                    if normalized_entry != nil
                        put_last!(out_map >> "entries", normalized_entry)
                    end
                end
                return out_map
            end
        end

        /// implicit object form: keys become items (skip meta keys like 'class'/'entries')
        top_keys = keys(raw_map)
        for top_key in top_keys
            if top_key == "class" or top_key == "entries"
                skip
            end

            top_value = raw_map[top_key]
            tv_type   = valtype(top_value)

            /// (A) Simple "Label: href" pair
            if tv_type == "str"
                put_last!(out_map >> "entries", {
                    "label": scout_as_str(top_key),
                    "href":  scout_as_str(top_value),
                    "children": []
                })
                skip
            end

            /// (B) Label-only item: value is nil (YAML null)
            if top_value == nil
                put_last!(out_map >> "entries", {
                    "label": scout_as_str(top_key),
                    "href":  "",
                    "children": []
                })
                skip
            end

            /// (C) Object item or group
            if tv_type == "map" <> tv_type == "object"
                direct_label = scout_as_str(top_key)
                if has(top_value, "label")
                    tmp_label = top_value >> "label"
                    if tmp_label != nil
                        direct_label = scout_as_str(tmp_label)
                    end
                end

                say "DEBUG: top_value type = " ++ valtype(top_value)
                say "DEBUG: top_value keys = " ++ string(keys(top_value))
                say "DEBUG: has href? = " ++ string(has(top_value, "href"))

                direct_href = ""
                if has(top_value, "href")
                    tmp_href = top_value["href"]
                    if tmp_href != nil
                        direct_href = scout_as_str(tmp_href)
                        say "DEBUG: after scout_as_str, direct_href = " ++ direct_href
                    else
                        say "DEBUG: tmp was nil!"
                    end
                end
                say "DEBUG canon: top_key=" ++ top_key ++ " direct_href=" ++ direct_href

                direct_class = ""
                if has(top_value, "class")
                    tmp_class = top_value["class"]
                    if tmp_class != nil
                        direct_class = scout_as_str(tmp_class)
                    end
                end

                child_entries = []
                if has(top_value, "children")
                    kids_any  = top_value >> "children"
                    kids_type = valtype(kids_any)
                    if kids_type == "array" <> kids_type == "seq"
                        for child_raw in kids_any
                            child_norm = scout_canon_entry(child_raw)
                            if child_norm != nil
                                put_last!(child_entries, child_norm)
                            end
                        end
                    end
                else
                    /// Treat remaining keys as children/groups, EXCLUDING meta keys
                    child_keys = keys(top_value)
                    for child_key in child_keys
                        if child_key == "label" or child_key == "href" or child_key == "children" or child_key == "class"
                            skip
                        end

                        child_value = top_value[child_key]
                        cv_type     = valtype(child_value)

                        /// child "Label: href"
                        if cv_type == "str"
                            put_last!(child_entries, {
                                "label": scout_as_str(child_key),
                                "href":  scout_as_str(child_value),
                                "children": []
                            })
                            skip
                        end

                        /// child label-only (nil)
                        if child_value == nil
                            put_last!(child_entries, {
                                "label": scout_as_str(child_key),
                                "href":  "",
                                "children": []
                            })
                            skip
                        end

                        /// child group → grandchildren (uses [] indexing only)
                        if cv_type == "map" <> cv_type == "object"
                            grand_entries = []

                            grand_keys = keys(child_value)
                            for grand_key in grand_keys
                                if grand_key == "label" or grand_key == "href" or grand_key == "children" or grand_key == "class"
                                    skip
                                end

                                grand_val  = child_value[grand_key]
                                grand_type = valtype(grand_val)

                                /// Case: "Label: href"
                                if grand_type == "str"
                                    put_last!(grand_entries, {
                                        "label": scout_as_str(grand_key),
                                        "href":  scout_as_str(grand_val),
                                        "children": []
                                    })
                                    skip
                                end

                                /// Case: nested group (e.g., Echo of Legends: { Chapter 1: ... })
                                if grand_type == "map" <> grand_type == "object"
                                    inner_entries = []
                                    inner_keys = keys(grand_val)
                                    for inner_key in inner_keys
                                        if inner_key == "label" or inner_key == "href" or inner_key == "children" or inner_key == "class"
                                            skip
                                        end
                                        inner_val  = grand_val[inner_key]
                                        inner_type = valtype(inner_val)

                                        if inner_type == "str"
                                            put_last!(inner_entries, {
                                                "label": scout_as_str(inner_key),
                                                "href":  scout_as_str(inner_val),
                                                "children": []
                                            })
                                            skip
                                        end

                                        if inner_val == nil
                                            put_last!(inner_entries, {
                                                "label": scout_as_str(inner_key),
                                                "href":  "",
                                                "children": []
                                            })
                                            skip
                                        end
                                    end

                                    put_last!(grand_entries, {
                                        "label": scout_as_str(grand_key),
                                        "href":  "",
                                        "children": inner_entries
                                    })
                                    skip
                                end

                                /// Case: label-only (nil)
                                if grand_val == nil
                                    put_last!(grand_entries, {
                                        "label": scout_as_str(grand_key),
                                        "href":  "",
                                        "children": []
                                    })
                                end
                            end

                            put_last!(child_entries, {
                                "label": scout_as_str(child_key),
                                "href":  "",
                                "children": grand_entries
                            })
                        end
                    end
                end

                put_last!(out_map >> "entries", {
                    "label": direct_label,
                    "href":  direct_href,
                    "children": child_entries
                })
            end
        end

        return out_map
    end

    /// Any other root shape → empty
    return out_map
end

/// ---- 3) render entries → HTML (no classes anywhere) ----------------------
act scout_render_list(entries_list, depth_level)
    if depth_level > 6
        return ""
    end
    
    local html_parts = []
    put_last!(html_parts, "<ul role=\"list\">")

    for item_any in entries_list
        if item_any == nil
            skip
        end

        local label    = item_any >> "label"
        local href     = item_any >> "href"
        local children = item_any >> "children"

        judge using item_any
            has(item_any, "children") and children.len > 0:
                local nested_html = scout_render_list(children, depth_level + 1)
                put_last!(html_parts, """
<li><details class="dropdown"><summary>{label}</summary>
{nested_html}
</details></li>
""")

            has(item_any, "href") and href !== "":
                local html_href = scout_to_html_href(href)
                put_last!(html_parts, """<li><a href="{html_href}">{label}</a></li>""")

            has(item_any, "label"):
                put_last!(html_parts, """<li><span>{label}</span></li>""")

            valtype(item_any) == "array" and item_any.len >= 2:
                local label_text = scout_as_str(item_any[0])
                local href_text  = scout_as_str(item_any[1])
                if label_text !== ""
                    local html_href = scout_to_html_href(href_text)
                    put_last!(html_parts, """<li><a href="{html_href}">{label_text}</a></li>""")
                end

            else:
                skip
        end
    end

    put_last!(html_parts, "</ul>")
    return pack(html_parts)
end

/// ---- 4) publish tokens (HTML already rendered; also register as token) ----
act publish_tokens()
    nav_root_dir = "modules/scout/navs"
    yaml_paths = []

    for discovered_yaml_path in walk(nav_root_dir, "**/*.yaml")
        put_last!(yaml_paths, discovered_yaml_path)
    end
    for discovered_yml_path in walk(nav_root_dir, "**/*.yml")
        put_last!(yaml_paths, discovered_yml_path)
    end

    say "Scout: root = " ++ nav_root_dir
    say "Scout: found " ++ string(yaml_paths.len) ++ " nav file(s)"

    for source_path in yaml_paths
        file_name = basename(source_path)
        file_stem = stem(file_name)
        token_key = upper(file_stem)

        say "Scout: parsing " ++ source_path
        yaml_text   = read_text(source_path)
        parsed_map  = yaml_parse(yaml_text)

        canonical_map = scout_canon_map(parsed_map)
        entries_array = canonical_map >> "entries"

        say "Scout: canon ok ->  " ++ token_key ++ " (entries=" ++ string(entries_array.len) ++ ")"

        html_menu = scout_render_list(entries_array, 0)

        say "Scout: render ok ->  " ++ token_key ++ " (chars=" ++ string(html_menu.len) ++ ")"

        partial_output_path = scout_partial_path(token_key)
        scout_create_all_dirs(partial_output_path)
        write_text!(partial_output_path, html_menu)

        register_token("SCOUT", token_key, html_menu)
        say "Scout: registered SCOUT:: " ++ token_key
    end

    return Unit
end